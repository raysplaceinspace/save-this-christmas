pub prop World.AnyHeroMoved
pub prop hero.FlipHero
pub prop hero.IsHeroWalking

pub collect hero.HeroSpeedBoost ~ Sum

const SantaWalkingImages = @santa*.png
const SantaStandingImage = @santa3.png

pub fn hero.Hero(pos, [owner]) {
    use body=hero
    use radius=TileRadius, shape=Circle, layer=Layer:Hero
    use speed=10
    
    Body(pos=, noRotation=true)
    PolygonCollider(category=Category:Hero)

    with FlipHero, IsHeroWalking {
        ImageSprite(
            IsHeroWalking ? SantaWalkingImages : SantaStandingImage,
            frameInterval=0.075s,
            radius=1.5*radius, screenOffset=@(0,-0.5),
            bodyScale=FlipHero ? @(-1,1) : @(1,1),
            shadow=0.25,
        )
    }
    
    DecaySpeed(1)

    on ButtonDown(Tap) {
        const MinDragDistance = 0.5*TileRadius
        behavior<dragControls> on BeforePhysics {
            let delta = Pointer - Pos
            if delta.Length >= MinDragDistance {
                Joystick = delta.Direction
            } else {
                Joystick = @(0,0)
            }
        }
    }
    on ButtonUp(Tap) {
        delete behavior<dragControls>
        Joystick = @(0,0)
    }

    on BeforePhysics {
        if Joystick == @(0,0) {
            IsHeroWalking = false
            continue
        }

        use speed = speed * (1 + HeroSpeedBoost)
        
        // So touchscreen players don't walk slower than keyboard players all the time
        let magnitude = Joystick.Length >= 0.1 ? 1 : Joystick.Length
        
        // Quantize to the grid so there is less walking into walls and getting snagged
        let angle = Joystick.Angle.Quantize(0.125rev)
        
        ForcefulStep(angle.Direction * magnitude * (speed/1s))
        IsHeroWalking = true
        AnyHeroMoved = true
        
        if Joystick.X != 0 {            
            FlipHero = Joystick.X > 0
        }
    }
}

pub fn spawner.HeroSpawner(pos) {
    SpawnEachPlayer {
        Subspawn hero {
            DespawnBefore(spawner)
            Hero(pos=)

            HeroEntranceStencil
        }
    }
}

pub fn hero.HeroEntranceStencil([owner]) {
    use body=hero
    use birth=Tick, duration=3s

    with Tick {
        let proportion = ((Tick - birth) / duration).Clamp(0, 1)
        if proportion < 1 {
            PolygonStencil<entrance>(Circle(radius=50*proportion), audience=owner)
        } else {
            delete PolygonStencil<entrance>
        }
    }
}