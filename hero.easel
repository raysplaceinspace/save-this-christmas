pub prop World.AnyHeroMoved
pub prop hero.FlipHero
pub prop hero.IsHeroWalking

const SantaWalkingImages = @santa*.png
const SantaStandingImage = @santa3.png

pub fn hero.Hero(pos, [owner]) {
    use body=hero
    use radius=TileRadius, shape=Circle, layer=Layer:Hero
    use speed=10
    
    Body(pos=, noRotation=true)
    PolygonCollider(category=Category:Hero)

    with FlipHero, IsHeroWalking {
        ImageSprite(
            IsHeroWalking ? SantaWalkingImages : SantaStandingImage,
            frameInterval=0.075s,
            radius=1.5*radius, screenOffset=@(0,-0.5),
            bodyScale=FlipHero ? @(-1,1) : @(1,1),
            shadow=0.25,
        )
    }
    
    DecaySpeed(1)

    on BeforePhysics {
        if Joystick == @(0,0) {
            IsHeroWalking = false
            continue
        }
        AnyHeroMoved = true
        ForcefulStep(Joystick * (speed/1s))
        IsHeroWalking = true
        
        if Joystick.X != 0 {            
            FlipHero = Joystick.X > 0
        }
    }
}

pub fn spawner.HeroSpawner(pos) {
    SpawnEachPlayer {
        Subspawn hero {
            DespawnBefore(spawner)
            Hero(pos=)
        }
    }
}