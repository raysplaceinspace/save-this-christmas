prop World.FetchedLeaderboard

pub page fn owner.HomePage {
    Music(@youShallNotPassTheParcel.mp3, loop=true)
    
    Toolbar {
        VolumeToggle
    }

    ToolbarRight {
        if IsUserLoggedIn {
            FlatButton(ManageAccountIntent, backgroundColor=#fff1) { %(PlayerName) }
        } else {
            FlatButton(LoginIntent, backgroundColor=Color:Primary) { "Login" }
        }
    }
    
    ContentScreen(expand=false) {
        VStack(gap=0, zStack=true) {
            VStack(backgroundColor=#0008, padding=1, zStack=true) {
                Blank(height=3)
                    
                HStack(gap=1, align=Align:Center) {
                    Image(@grouch1.png, height=8)
                    Image(@grouch2.png, height=6)
                    Image(@grouch3.png, height=4)
                    Blank(width=2)
                    Image(@santa3.png, height=10)
                    Blank(width=2)
                    Image(@gift1.png, height=3)
                    Image(@gift1.png, height=5)
                    Image(@gift1.png, height=7)
                }

                Standout(margin=0, animate=Animate:Throb) { "You Shall Not Pass..." }
                H1(margin=0) { "...the Parcel" }

                Blank(height=1)

                P(fontSize=1.2) { "A game about building barricades to save Christmas" }

                Blank(height=1)
            }
               
            VStack(backgroundColor=#0004, align=Align:Center, vPadding=1) {
                P(fontSize=0.9) { "Choose Your Name" }
                VStack(width=30) {
                    PlayerNameEditor
                }
                RaisedButton(Main, backgroundColor=Color:Primary, width=30, fontSize=1.5) { "Play" }
            }
        }
        P(fontSize=0.9, color=#aaa) {
            "This game was made with "
            Link("https://easel.games") { "Easel" }
            " - the beginner-friendly programming language for 2D multiplayer games!"
        }
    }

    Content {
        VStack(align=Align:Center) {
            H2 { "Leaderboard" }

            if !IsUserLoggedIn && OverallBestScore > 0 {
                ShinyPanel(align=Align:Center, vPadding=0.5, backgroundColor=#f60) {
                    P(fontSize=0.8) {
                        Icon("fa-solid fa-triangle-exclamation")
                        " Unregistered high scores get deleted after 90 days. To keep your high score forever, please "
                        Link(LoginIntent) { "login" }
                        "."
                    }
                }
            }

            with FetchedLeaderboard, PlayerName {
                if FetchedLeaderboard {
                    let foundMyself = false
                    let rank = 1
                    for player in FetchedLeaderboard %%{
                        let isMe = player.UserId == owner.UserId
                        if isMe { foundMyself = true }
                        
                        ShinyPanel(backgroundColor = isMe ? #fa0 : #b0f, vPadding = isMe ? 0.6 : 0.4) {
                            HStack {
                                P(fontSize=0.75, color=#eee, width=1.5, align=Align:Center) {
                                    %(rank)
                                }
                                P(fontSize=isMe ? 1.5 : 1.2, bold=true) {
                                    PlayerNameDisplay(isMe ? owner : player, color=#fff)
                                }
            
                                Blank(expand=true)
            
                                if player.BestNumGiftsRemaining >= MaxNumGifts {
                                    P { %("All gifts saved in " + (player.BestDuration/1m).FormatWithDecimals(2) + " minutes" ) }
                                } else if player.BestNumGiftsRemaining > 0 {
                                    P { %(player.BestNumGiftsRemaining + " gifts saved") }
                                } else {
                                    P { %(player.BestNumWavesSurvived + " waves survived") }
                                }
                            }
                        }
            
                        rank += 1
                    }
            
                    if FetchedLeaderboard.IsEmpty {
                        P { "No one on the leaderboard... yet" }
                    }
                } else {
                    P { "Loading..." }
                }
            }
        }
    }

    once FetchLeaderboard([&OverallBestScore, &BestNumGiftsRemaining, &BestNumWavesSurvived, &BestDuration]) rankings {
        FetchedLeaderboard = rankings
    }
}