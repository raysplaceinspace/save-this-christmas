pub game fn World.Main() {
    SolidBackground(color=#282228)
    Camera(body=@(0,0), radius=@(BoundaryRadiusX, BoundaryRadiusY))

    ButtonRemap(KeyW, ArrowUp)
    ButtonRemap(KeyA, ArrowLeft)
    ButtonRemap(KeyS, ArrowDown)
    ButtonRemap(KeyD, ArrowRight)
    
    KeyboardVirtualJoystick(up=ArrowUp, left=ArrowLeft, right=ArrowRight, down=ArrowDown)
    TouchscreenVirtualPointer

    SubspawnTileMap(TileMap1)
    let maxNumGifts = AllGifts.Count

    let start = Tick
    await PlayRounds

    for use owner in QueryPlayers {
        use this=owner
        
        let isNewHighScore = SubmitToLeaderboard(
            numWavesSurvived=NumWavesSurvived,
            numGiftsRemaining=AllGifts.Count,
            duration=Tick - start,
        )
    
        OverlayContent {
            VStack(align=Align:Center) {
                H1 {
                    FlyInWords(AllGifts.IsEmpty ? "Christmas is lost!" : "You saved Christmas!")
                }
    
                InsetPanel {
                    P {
                        "Christmas presents saved: " + AllGifts.Count + " out of " + maxNumGifts
                    }
                    P {
                        "Waves survived: " + NumWavesSurvived
                    }
                }
                
                if isNewHighScore {
                    P(bold=true, color=#e580ff, fontSize=1.4) {
                        "New high score!"
                    }
                }
    
                if AllGifts.IsEmpty {
                    P {
                        Span(bold=true) { "Tip: " }
                        "Use the wooden crates to make a perimeter around your Christmas gifts"
                    }
                }

                HStack(align=Align:Center) {
                    RaisedButton(Main, backgroundColor=Color:Primary) { "Play Again" }
                    RaisedButton(LeaderboardPage, backgroundColor=Color:Secondary) { "High Scores" }
                }
            }
        }
    }
}