pub fn powerup.CandyCane(pos) {
    use body=powerup
    use radius=TileRadius
    use layer=Layer:Powerup
    
    Body(pos=, noRotation=true)
    PolygonCollider(
        shape=Circle(radius=0.25*radius), // Small collider so you don't accidentally pick it up
        category=Category:Powerup,
        collideWith=Category:Tangible ^ Category:Monster,
    )
    ImageSprite(@candy-cane.png, shadow=0.5)

    on BeforeCollide that {
        if that.Category.Overlaps(Category:Hero) {
            use hero=that
            hero.Subspawn {
                use duration=10s
                give HeroSpeedBoost(0.5)

                let end = Tick + duration
                BottomContent {
                    Panel(use width=25, use height=2, padding=0) {
                        HStack(align=Align:Center) {
                            with Tick {
                                let proportion = (end - Tick).Max(0) / duration
                                if proportion <= 0 { Expire }
    
                                FlatPanel(width=proportion*width, height=, padding=0, backgroundColor=#fc0) { }
                            }
                        }

                        ZOverlay { "Speed Boost" }
                    }
                }
                once Tick(10s) { Expire }
            }
            Expire
        }
    }
}